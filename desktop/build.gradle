buildscript {
    dependencies {
        classpath "org.openjfx:javafx-plugin:0.0.8"
    }
}

plugins {
    id "com.github.spotbugs" version "4.6.0"
}

project.setDescription('Parts of GeoGebra related to desktop platforms')

apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'


if (JavaVersion.current() == JavaVersion.VERSION_11) {
    apply plugin: 'org.openjfx.javafxplugin'
    javafx {
        version = "11.0.2"
        modules = [ 'javafx.controls', 'javafx.web', 'javafx.swing' ]
    }
}

sourceSets {
    nonfree
    gpl
    main {
        resources {
            if (project.hasProperty("usegpl")) {
                srcDirs += gpl.resources.srcDirs
            } else {
                srcDirs += nonfree.resources.srcDirs
            }
        }
    }
    e2eTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output
    }
}

configurations {
    e2eTestCompile.extendsFrom testCompile
    e2eTestCompile.extendsFrom testRuntime
}

eclipse.classpath.sourceSets.removeAll { it.name == 'nonfree' || it.name == 'gpl'}
eclipse.classpath.file {
	// Eclipse dependecies are based on projects, not source paths
	whenMerged { classpath ->
		classpath.entries.removeAll { entry ->
			entry.kind == 'lib' && entry.path.matches(".*common-jre/build/.*/test") 
		}
	}
}

File javaHome = new File(System.properties['java.home'])
javaHome = javaHome.name == 'jre' ? javaHome.parentFile : javaHome
String jfxrtLocation = new File("${javaHome}/jre/lib/jfxrt.jar").absolutePath
// JavaFX locations for JDK7, JDK7, JDK8
for (location in ['lib/jfxrt.jar', 'jre/lib/jfxrt.jar', 'jre/lib/ext/jfxrt.jar']) {
    File file = new File(javaHome, location)
    if (file.exists()) {
        jfxrtLocation = file.absolutePath
        break
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

List ext = [
    'netscape.javascript:jsobject:1', 
    'com.apple:mac_extensions:20040714', 
    'com.ogprover:OpenGeoProver:20120725',
    'com.sun:jna:4.1.0']


dependencies {
    compile project(':common')
    compile project(':common-jre')
    compile project(':editor-desktop')
    compile project(':jogl2')
    compile project(':input3D:impl')
    compile project(':giac-jni')
    compile project(':tarski-jni')
    compile ext, files(jfxrtLocation)

    runtime "com.jogamp:jogl-all:$joglVersion:natives-linux-amd64",
        "com.jogamp:jogl-all:$joglVersion:natives-linux-armv6hf",
        "com.jogamp:jogl-all:$joglVersion:natives-windows-amd64",
        "com.jogamp:jogl-all:$joglVersion:natives-macosx-universal"
    runtime "com.jogamp:gluegen-rt:$joglVersion:natives-linux-amd64",
        "com.jogamp:gluegen-rt:$joglVersion:natives-linux-armv6hf",
        "com.jogamp:gluegen-rt:$joglVersion:natives-windows-amd64",
        "com.jogamp:gluegen-rt:$joglVersion:natives-macosx-universal"
    /*
    runtime "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-linux-amd64",
        "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-linux-i586",
        "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-windows-amd64",
        // "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-linux-arm",
        "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-windows-i586",
        "fr.ujf-grenoble:javagiac:$javaGiacVersion:natives-macosx-universal"
    */

    runtime "tk.tarski:javagiac:$javaGiacVersion:natives-linux-amd64",
            "tk.tarski:javagiac:$javaGiacVersion:natives-macosx-universal",
            "tk.tarski:javagiac:$javaGiacVersion:natives-windows-amd64",
            "tk.tarski:javagiac:$javaGiacVersion:natives-linux-arm"

    runtime "tk.tarski:javatarski:$javaTarskiVersion:natives-linux-amd64",
            "tk.tarski:javatarski:$javaTarskiVersion:natives-windows-amd64",
            "tk.tarski:javatarski:$javaTarskiVersion:natives-macosx-universal",
            "tk.tarski:javatarski:$javaTarskiVersion:natives-linux-arm"

    testCompile project(':keyboard-base')
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.hamcrest:hamcrest:$hamcrestVersion"
    testCompile project(':ggbjdk')
    testCompile project(path: ':common-jre', configuration: 'testArtifacts')
}

mainClassName = "org.geogebra.desktop.GeoGebra3D"

run {
    // Copying JOGL related native JARs into the same directory where the non-native JAR takes place.
    // JOGL is simply dumb, it cannot work neither with java.library.path nor classpath or anything. Arrgh. 
    def gluegen = project.configurations.compile.find { it.name == "gluegen-rt-${joglVersion}.jar" }
    def gluegen_natives = project.configurations.runtime.findAll { it.name.startsWith("gluegen-rt-$joglVersion-natives") }
    def gluegen_dir = gluegen.getParent()
    for (i in gluegen_natives) {
        def gluegen_native_path = i.getPath()
        ant.copy(file:"$gluegen_native_path", todir:"$gluegen_dir")
    }
    def jogl = project.configurations.compile.find { it.name == "jogl-all-${joglVersion}.jar" }
    def jogl_natives = project.configurations.runtime.findAll { it.name.startsWith("jogl-all-$joglVersion-natives") }
    def jogl_dir = jogl.getParent()
    for (i in jogl_natives) {
        def jogl_native_path = i.getPath()
        ant.copy(file:"$jogl_native_path", todir:"$jogl_dir")
    }

    // This should be done in a different way:
    systemProperty "java.library.path", "${project.projectDir}/build"
}
test {
    systemProperty "ggb.prerelease", project.findProperty("prerelease") ?: "true";

    ignoreFailures = true
    outputs.upToDateWhen { false }
}
spotbugs {
    ignoreFailures = true
    excludeFilter = file("config/findbugs.xml")
    jvmArgs = ['-Dfindbugs.sf.comment=true']
}

import org.gradle.plugins.ide.eclipse.model.AccessRule
eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('accessible', 'com/**'))
                jre.accessRules.add(new AccessRule('accessible', 'javafx/**'))
            }
        }
    }
}

checkstyle {
    configFile file("config/checkstyle/checkstyle.xml")
}

task e2eTest(type: Test) {
    description 'Run end-to-end tests'
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}

checkstyle {
    configDir = file("config/checkstyle/")
    toolVersion = "8.8"
}

// https://gist.github.com/jlmelville/2bfe9277e9e2c0ff79b6#gistcomment-2802097
tasks.withType(CreateStartScripts).each { task ->
    task.doLast {
        task.windowsScript.write task.windowsScript.text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
        task.unixScript.write task.unixScript.text.replaceFirst(/(CLASSPATH=.APP_HOME\/lib\/).*/, { "${it[1]}*" })
    }
}
